plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'de.netbeacon.purrito'
version '2.0.5'
sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.1'
    // https://mvnrepository.com/artifact/org.json/json
    implementation group: 'org.json', name: 'json', version: '20210307'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
}

shadowJar {
    archiveClassifier.set("")
}

publishing {
    repositories {
        maven {
            credentials {
                username System.getenv('nexus_hypercdn_user')
                password System.getenv('nexus_hypercdn_password')
            }
            url "https://nexus.hypercdn.de/repository/hypercdn-maven/"
        }
    }
    publications {
        maven(MavenPublication) {
            artifact(tasks["shadowJar"])
            pom {
                name = 'PurrBot API Wrapper'
                description = 'A simple wrapper for the anime image api provided by PurrBot'
                url = 'https://github.com/Horstexplorer/PurrBot-API-Wrapper'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'horstexplorer'
                        name = 'horstexplorer'
                        email = 'null@null.null'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Horstexplorer/PurrBot-API-Wrapper'
                    developerConnection = 'scm:git:ssh:git@github.com:Horstexplorer/PurrBot-API-Wrapper'
                    url = 'https://github.com/Horstexplorer/PurrBot-API-Wrapper'
                }
            }
        }
    }
}