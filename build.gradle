plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'de.netbeacon.purrito'
version '3.0.0.' + System.getenv("BUILD_NUMBER")
sourceCompatibility = 17
targetCompatibility = 17

repositories {
    maven {
        name 'maven-hypercdn'
        url 'https://nexus.hypercdn.de/repository/maven-hypercdn'
    }
    mavenCentral()
}

dependencies {

    implementation group: 'de.hypercdn.commons', name: 'Commons', version: '1.0.4.28'

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'hours'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'hours'
}

shadowJar {
    archiveClassifier.set("")
}

publishing {
    repositories {
        maven {
            credentials {
                username findProperty("nexusu") as String
                password findProperty("nexusp") as String
            }
            url "https://cd.voigt.app/repository/maven-voigt/"
        }
    }
    publications {
        maven(MavenPublication) {
            artifact(tasks["shadowJar"])
            pom {
                name = 'Purrito'
                description = ''
                url = 'https://github.com/Horstexplorer/PurrBot-API-Wrapper'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Horstexplorer/PurrBot-API-Wrapper'
                    developerConnection = 'scm:git:ssh:git@github.com:Horstexplorer/PurrBot-API-Wrapper'
                    url = 'https://github.com/Horstexplorer/PurrBot-API-Wrapper'
                }
            }
        }
    }
}